buildscript {
    ext {
        springBootVersion = '2.1.7.RELEASE'
        springCloudVersion = 'Greenwich.SR2'
        springCloudAlibabaVersion = '0.9.0.RELEASE'
        javaLanguageLevel = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = 'com.imooc'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'org.springframework.boot'
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = rootProject.javaLanguageLevel
    targetCompatibility = rootProject.javaLanguageLevel

    jar { enabled = true }
    bootJar { enabled = false }

    dependencies {
        implementation 'org.codehaus.groovy:groovy'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }
}